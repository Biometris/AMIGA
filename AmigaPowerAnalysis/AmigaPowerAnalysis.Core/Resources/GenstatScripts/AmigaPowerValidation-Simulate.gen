" Directories to check
  endpoint: number of endpoint;     negative number implies all
  reps:     number of replication;  negative number implies all
  effects:  number of effect;       negative number implies all
  datasets: number of datasets;     negative number implies all
"

" Attach procedure file "
sfilename channel=1 ; filetype=input ; directory=thisdir
txconstru [profile] thisdir, 'AmigaPowerValidation-Simulate.pro'
scalar    channel ; *
open      profile ; channel=channel ; filetype=input ; width=100
input     [print=*] channel
close     channel=channel ; filetype=input

" Check whether this is called from R "
enquire   channel=2 ; filetype=input ; open=open2 ; name=name2
if (open2.eq.0)
    " Call from GenStat/CRiSP"
    text      maindir  ; 'D:\\Diverse Opdrachten\\AMIGA Power analysis\\'
    concatena maindir, 'TestData\\Simple\\'
    variate   endpoint ; !(1)
    variate   reps     ; !(1)
    variate   effects  ; !(2)
    variate   datasets ; !(2,7,5)
    text      dir[1] ; 'Simple-Wald'
    \text      dir[2] ; 'Simple-LR'
    \text      dir[3] ; 'SimpleBlock-Wald'
    \text      dir[4] ; 'SimpleBlock-LR'
  else
    " Called from C#: loop over endpoint defined by input on channel 2 "
    " Loop over all reps/effects/datasets "
    text      maindir  ; ''
    variate   reps     ; !(-1)
    variate   effects  ; !(-1)
    variate   datasets ; !(-1)
    if 0
      text      name2 ; 'D:\\Diverse Opdrachten\\AMIGA Power analysis\\'
      concatena name2, 'TestData\\Simple\\Simple-Wald\\0-Settings.csv'
    endif
    sfilename [inputname=name2] surname=surname ; directory=dir[1]
    concatena surname ; width=getposition(surname ; '-') - 1
    read      [print=* ; channel=surname] endpoint
    close     channel=surname
    print     dir[1], endpoint ; decimals=0
    variate   endpoint ; !(-1)
endif

" Additional settings "
text      print ; !t(NB,PVALUES)
text      print ; !t(PVALUES)
scalar    test ; 0

" Ensure that last character of directory is a slash (\) "
for dd=dir[]
  txconstru [dd] maindir, dd, '\\'
  sreplace  [!t('\\\\') ; !t('\\')] dd
endfor

" Create cases for looping "
calculate ndir = nvalues(dir)
for [ntimes=ndir ; index=ii]
  if (min(endpoint) .lt. 0)
      dirlist   [print=* ; directory=dir[ii]] '*-Settings.csv' ; name=files
      calculate nfiles = nvalues(files)
      variate   iendpoint[ii] ; !(1...nfiles) - 1 ; deci=0
    else
      calculate nfiles = 1
      variate   iendpoint[ii] ; !(#endpoint) ; deci=0
  endif
  text      [values=#nfiles(#dir[ii])] idir[ii]
endfor
text      allDir  ; !t(#idir)
variate   allEndpoint ; !(#iendpoint) ; deci=0
print     allDir, allEndpoint

" Loop over All cases "
calculate ndir = nvalues(allDir)
for [ntimes=ndir ; index=ii]
  text      iidir  ; allDir$[ii]
  scalar    endp ; allEndpoint$[ii]
  am2_get2  iidir ; iidir2
  txconstru [caption] iidir2, '  Endpoint ', endp, ' ' ; deci=0
  txpad     [padd='=' ; method=after] caption ; width=86
  caption   caption ; style=meta
  AM1_POWER [pr=#print ; test=test] iidir ; endp ; reps ; effects ; \
            datasets ; maxDiff[ii]
endfor

" maximal Difference over All endpoints "
calculate nmx, nmx1 = nvalues(maxDiff[1]) - (0,1)
pointer   [nvalues=nmx1] extra
getattrib [attribute=extra] maxDiff[1][1...nmx1] ; extra[]
pointer   [nvalues=nmx1] mx, max
variate   mx[] ; extra=extra[][]
scalar    max[] ; extra=extra[][]
for [ntimes=nmx1 ; index=ii]
  calculate mx[ii] = vmax(!p(maxDiff[][ii]))
endfor
calculate max[] = max(mx[])
for
  print     [mis='-' ; iprint=extra] maxDiff[1][0],mx[] ; \
            field=-9 ; deci=1 ; skip=0,2,3(0),2,3(0)
  print     [mis='-' ; iprint=extra] 'Global',max[] ; \
            field=-9 ; deci=1 ; skip=0,2,3(0),2,3(0)
endfor

" Output to CSV in case output 2 is set"
enquire   channel=2 ; filetype=output ; open=open2 ; name=name2
if open2
  close     channel=2 ; filetype=output ; delete=yes
  export    [outfile=name2 ; method=overwrite] max[] ; columns=extra[][]
endif
stop
